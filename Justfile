# Default command lists all available recipes
_default:
    @just --list

alias b := bump
alias c := clean
alias d := dist
alias h := hooks
alias q := check
alias t := test

PROJ := `uv version --short`

# lint python code using ruff
[group("style")]
check-lint:
    uv run ruff check . --fix

# format python code using ruff
[group("style")]
check-format:
    uv run ruff format .

# run the type checker ty
[group("style")]
check-types:
    uv run ty check

# lint, format with ruff and type-check with ty
[group("style")]
check: check-lint check-format check-types

# run tests with coverage
[group("test")]
test:
    uv run pytest tests/

# run tests for all the supported Python versions
[group("test")]
testall:
    uv run --python=3.10 pytest
    uv run --python=3.12 pytest
    uv run --python=3.14 pytest

# run all the formatting, linting, and testing commands
[group("test")]
ci PYTHON="3.12":
    uv run --python={{ PYTHON }} ruff format .
    uv run --python={{ PYTHON }} ruff check . --fix
    uv run --python={{ PYTHON }} ty check .
    uv run --python={{ PYTHON }} pytest tests/

# setup the pre-commit hooks
[group("repo")]
hooks:
    uvx prek install

# print the current status of the project
[group("repo")]
status:
    @echo "Project Version: {{ PROJ }}"
    @echo "Running on: `uname`"

# clean all python build/compilation files and directories
[group("repo")]
clean: clean-build clean-pyc clean-test

# remove build artifacts
[private]
clean-build:
    rm -fr build/
    rm -fr site/
    rm -fr dist/
    rm -fr .eggs/
    find . -name '*.egg-info' -exec rm -fr {} +
    find . -name '*.egg' -exec rm -f {} +

# remove Python file artifacts
[private]
clean-pyc:
    find . -name '*.pyc' -exec rm -f {} +
    find . -name '*.pyo' -exec rm -f {} +
    find . -name '*~' -exec rm -f {} +
    find . -name '__pycache__' -exec rm -fr {} +

# remove test and coverage artifacts
[private]
clean-test:
    rm -f .coverage
    rm -fr htmlcov/
    rm -fr .pytest_cache

# install dependencies in local venv
[group("repo")]
venv:
    uv sync --all-extras --all-groups

# build the source distribution and wheel file with uv (bump version first)
[group("repo")]
dist:
    uv build

# serve the documentation
[group("repo")]
docs:
    uv sync --group docs
    uv run zensical serve

# initialize a git repo and add all files
[group("chore")]
init: venv
    git init
    just hooks
    git add .
    git commit -m "initial commit"
    @echo "{{ GREEN }}{{ BOLD }}Git has been initialized{{ NORMAL }}"

# write the changelog
[group("chore")]
changelog VERSION="auto":
    uvx git-changelog -Tio CHANGELOG.md -B="{{VERSION}}" -c angular


# bump the version, commit and tag (<major, minor, patch> or semver)
[group("chore")]
bump VERSION: && tag
    uv version  {{ VERSION }}
    uv lock

# tag the latest version
[group("chore")]
[private]
tag VERSION=`uv version --short`:
    git add pyproject.toml
    git add uv.lock
    git commit -m "chore: bumped version to {{VERSION}}"
    git tag -a "v{{VERSION}}"

# make a new release (e.g. "just release 0.1.2") (repo must be clean)
[group("chore")]
release VERSION: test
    @just changelog "v{{VERSION}}"
    git add CHANGELOG.md
    git commit -m "chore: updated Changelog"
    @just bump "{{VERSION}}"
    @echo "{{GREEN}}Success! Run 'git push && git push --tags' now.{{NORMAL}}"
